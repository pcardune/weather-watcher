path / {
  read() { true }
  write() { false }
}

path /comparisonPoints/{comparisonPointId} is ComparisonPoint {
  write() {
    createOnly(this) || (
      writeOnce(this.id) &&
      writeOnce(this.name) &&
      writeOnce(this.latitude) &&
      writeOnce(this.longitude) &&
      writeOnce(this.placeId)
    )
  }
}

type ComparisonPoint {
 id: String,
 name: String,
 latitude: Number,
 longitude: Number,
 placeId: String | Null,
 noaaPointId: String | Null,
}

path /comparisons/{comparisonId} is Comparison {
  write() {
    (this == null && isCurrentUser(prior(this.creator))) || (isCurrentUser(this.creator) &&
    (this == null || (
      writeOnce(this.id) &&
      writeOnce(this.creator) &&
      writeOnce(this.createdOn)
    )))
  }
  index() {
    ["creator"]
  }
}

path /comparisons/{comparisonId}/comparisonPointIds {
  write() {
    isCurrentUser(prior(root).comparisons[comparisonId].creator)
  }
}

type Comparison {
 id: String,
 name: String,
 creator: String,
 createdOn: Number,
}

createOnly(value) { prior(value) == null && value != null }
writeOnce(value) { prior(value) == null || value == prior(value) }
isSignedIn() { auth != null }
isCurrentUser(uid) { isSignedIn() && auth.uid == uid }
